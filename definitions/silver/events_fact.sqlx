config { 
    type: "incremental",
    schema: dataform.projectConfig.vars.silver_Schema,
    tags: ["events" ,"silver"],
    columns: {
      ip_address: {
        bigqueryPolicyTags: ["projects/gcp-sandbox-3-393305/locations/us-central1/taxonomies/6103638487414924910/policyTags/6256338910151106080"]
      }
    },
    assertions: {
    uniqueKey: ["id"],
    nonNull: ["session_id"],
    rowConditions: [
      'ip_address is not null',
      'traffic_source in ("Facebook", "Adwords", "Email", "Organic", "YouTube")'
    ]
    }   
 } 
SELECT id,
userid,
sequence_number,
session_id,
PARSE_DATETIME('%Y-%m-%d %H:%M:%E*S UTC', nullif(TRIM(created_at),'')) as created_at,
ip_address,
city,
state,
postal_code,
browser,
traffic_source,
uri,
event_type FROM ${ref("stg_events")}

${when(incremental(), `WHERE cast(substr(created_at, 1, 10) as date) > (SELECT MAX(EXTRACT(DATE FROM created_at)) FROM ${self()})`) }