config { 
    type: "incremental",
    uniqueKey: ["id"],
    schema: dataform.projectConfig.vars.silverSchema,
    name: "fact_order_items",
    tags: ["order_items" ,"silver"],
    description: "Order Items Fact Table",
    dependencies: ["order_items_nonnull_check","order_items_rowvalidations_check","order_items_uniquekey_check"],
    assertions: {
    uniqueKey: ["id"],
    nonNull: ["inventory_item_id", "user_id", "product_id"],
    rowConditions: [
      'status in ("Shipped", "Complete", "Cancelled", "Processing", "Returned")'
    ]
    }   
 } 
SELECT 
id,
order_id,
user_id,
product_id,
inventory_item_id,
status,
${utils.parse_date("created_at")} as created_at,
${utils.parse_date("shipped_at")} as shipped_at,
${utils.parse_date("delivered_at")} as delivered_at,
${utils.parse_date("returned_at")} as returned_at,
CAST(sale_price as NUMERIC) as sale_price
FROM ${ref("stg_order_items")}


${when(incremental(), `WHERE cast(substr(created_at, 1, 10) as date) > (SELECT IFNULL(MAX(EXTRACT(DATE FROM created_at)), CAST('${constants.DEFAULT_DATE}' as DATE)) FROM ${self()})`)}