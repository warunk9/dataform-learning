config { 
    type: "incremental",
    uniqueKey: ["order_id"],
    schema: dataform.projectConfig.vars.silverSchema,
    name: "fact_orders",
    tags: ["orders" ,"silver"],
    description: "Orders Fact Table",
    dependencies: ["assert_orders_nonnull","assert_orders_user_id_nonnull","assert_orders_rowvalidation"] 
 } 
SELECT 
order_id,
user_id,
status,
gender,
${utils.parse_date("created_at")} as created_at,
${utils.parse_date("returned_at")} as returned_at,
${utils.parse_date("shipped_at")} as shipped_at,
${utils.parse_date("delivered_at")} as delivered_at,
num_of_item
FROM ${ref("stg_orders")}

${when(incremental(), `WHERE cast(substr(created_at, 1, 10) as date) > (SELECT IFNULL(MAX(EXTRACT(DATE FROM created_at)), CAST('${constants.DEFAULT_DATE}' as DATE)) FROM ${self()})`)}