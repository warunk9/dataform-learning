config { 
    type: "incremental",
    schema: dataform.projectConfig.vars.silverSchema,
    name: "dim_inventory_items",
    tags: ["inventory_items" ,"silver"],
    description: "Inventory Items Dimension Table",
    columns: {
      product_name: {
        bigqueryPolicyTags: ["projects/gcp-sandbox-3-393305/locations/us-central1/taxonomies/6103638487414924910/policyTags/6256338910151106080"]
      }
    },
    bigquery: {
     labels: {"type":"dimension"}
  },
    dependencies: ["assert_inventory_items_rowvalidations"] 
 } 

SELECT 
  id, 
  product_id, 
  ${utils.parse_date("created_at")} as created_at, 
  ${utils.parse_date("sold_at")} as sold_at, 
  CAST(cost as NUMERIC) as cost, 
  product_category, 
  product_name, 
  product_brand, 
  product_retail_price, 
  product_department, 
  product_sku, 
  product_distribution_ceter_id FROM ${ref("stg_inventory_items")}
${when(incremental(), `WHERE cast(substr(created_at, 1, 10) as date) > (SELECT IFNULL(MAX(created_at), CAST('${constants.DEFAULT_DATE}' as DATE)) FROM ${self()})`)}
